/*
This is the RE/flex lexer specification
for the fluster language. RE/flex generates
efficient C++ lexing/scanning code from
this specification.
*/

%top{
  #include "parser.gen.h"
  //#include "driver.h"
  #include "lexer_routines.h"
  #include <iostream>
  #include <queue>
%}

 /* additional members of the Lexer class */
%class{
  std::queue<yy::Parser::symbol_type> token_queue;
  void pushToken(const yy::Parser::symbol_type& token);
public:
  yy::Parser::symbol_type getToken();
%}


 /* additional initialization of Lexer objects */
%init{
%}

%{
  void yy::Lexer::pushToken(const yy::Parser::symbol_type& token)
  {
    token_queue.emplace(token);
  }

  yy::Parser::symbol_type yy::Lexer::getToken()
  {
    if (token_queue.empty())
      pushToken(lex());

    auto result = token_queue.front();
    token_queue.pop();
    return result;
  }
%}

/*** Definitions ***/


id                  [_a-zA-Z][_a-zA-Z0-9]*
digit               [0-9]
bit                 [01]
hex                 [0-9a-fA-F]
octal               [0-7]
decnum_start        [1-9]+
decint              {decnum_start}{digit}*
any                 .|\n
ANY                 .|\n|<<EOF>>
string_content      {any}*[^\\]


/*** Options***/

%o debug

%o unicode
%o reentrant
%o noyywrap nounput noinput batch debug
%o namespace=yy
%o lexer=Lexer
%o bison-complete
%o bison-cc-parser=Parser
%o bison-locations

%o tabs=4

%o exception="yy::Parser::syntax_error(location(), \"unknown token.\")"

/*** States ***/

/* backslash handling states */
%s  INNER_BACKSLASH_READ_REST

%%


 /*** Operators ***/

"++"            return yy::Parser::make_PLUS_PLUS(location());
"--"            return yy::Parser::make_DASH_DASH(location());
"<"             return yy::Parser::make_LANGLE(location());
"<="            return yy::Parser::make_LANGLE_EQ(location());
"=="            return yy::Parser::make_EQ_EQ(location());
"#="            return yy::Parser::make_HASH_EQ(location());
"!#="           return yy::Parser::make_EXCLAIM_HASH_EQ(location());
"<=>"           return yy::Parser::make_LANGLE_EQ_RANGLE(location());
"~"             return yy::Parser::make_TILDE(location());
"!="            return yy::Parser::make_EXCLAIM_EQ(location());
"!"             return yy::Parser::make_EXCLAIM(location());
">="            return yy::Parser::make_RANGLE_EQ(location());
">"             return yy::Parser::make_RANGLE(location());
"+"             return yy::Parser::make_PLUS(location());
"-"             return yy::Parser::make_DASH(location());
"*"             return yy::Parser::make_STAR(location());
"/"             return yy::Parser::make_FSLASH(location());
"//"            return yy::Parser::make_FSLASH_FSLASH(location());
"%"             return yy::Parser::make_PERCENT(location());
"^"             return yy::Parser::make_CARET(location());
"^/"            return yy::Parser::make_CARET_FSLASH(location());
\<has\>         return yy::Parser::make_HAS(location());
\<is\>          return yy::Parser::make_IS(location());
"&"             return yy::Parser::make_AMPERSAND(location());
"|"             return yy::Parser::make_PIPE(location());
">>"            return yy::Parser::make_RANGLE_RANGLE(location());
"<<"            return yy::Parser::make_LANGLE_LANGLE(location());
"->"            return yy::Parser::make_DASH_RANGLE(location());
"="             return yy::Parser::make_EQ(location());
"+="            return yy::Parser::make_PLUS_EQ(location());
"-="            return yy::Parser::make_DASH_EQ(location());
"*="            return yy::Parser::make_STAR_EQ(location());
"/="            return yy::Parser::make_FSLASH_EQ(location());
"//="           return yy::Parser::make_FSLASH_FSLASH_EQ(location());
"^/="           return yy::Parser::make_CARET_FSLASH_EQ(location());
"^="            return yy::Parser::make_CARET_EQ(location());
"&="            return yy::Parser::make_AMPERSAND_EQ(location());
"|="            return yy::Parser::make_PIPE_EQ(location());
"~="            return yy::Parser::make_TILDE_EQ(location());
">>="           return yy::Parser::make_RANGLE_RANGLE_EQ(location());
"<<="           return yy::Parser::make_LANGLE_LANGLE_EQ(location());
"->="           return yy::Parser::make_DASH_RANGLE_EQ(location());
"."             return yy::Parser::make_DOT(location());
".."            return yy::Parser::make_DOT_DOT_DOT(location());
"..."           return yy::Parser::make_DOT_DOT_DOT(location());
"["             return yy::Parser::make_LBRACK(location());
"]"             return yy::Parser::make_RBRACK(location());
"{"             return yy::Parser::make_LBRACE(location());
"}"             return yy::Parser::make_RBRACE(location());
"("             return yy::Parser::make_LPAR(location());
")"             return yy::Parser::make_RPAR(location());
","             return yy::Parser::make_COMMA(location());
"=>"            return yy::Parser::make_EQ_RANGLE(location());
":"             return yy::Parser::make_COLON(location());
";"             return yy::Parser::make_SEMICOLON(location());
"@"             return yy::Parser::make_AT(location());
"?"             return yy::Parser::make_QUES(location());

 /*** Keywords ***/
\<in\>          return yy::Parser::make_IN(location());
\<mod\>         return yy::Parser::make_MOD(location());

\<if\>          return yy::Parser::make_KW_IF(location());
\<else\>        return yy::Parser::make_KW_ELSE(location());
\<elif\>        return yy::Parser::make_KW_ELIF(location());
\<for\>         return yy::Parser::make_KW_FOR(location());
\<while\>       return yy::Parser::make_KW_WHILE(location());
\<until\>       return yy::Parser::make_KW_UNTIL(location());
\<repeat\>      return yy::Parser::make_KW_REPEAT(location());
\<case\>        return yy::Parser::make_KW_CASE(location());
\<try\>         return yy::Parser::make_KW_TRY(location());
\<retry\>       return yy::Parser::make_KW_RETRY(location());
\<handle\>      return yy::Parser::make_KW_HANDLE(location());
\<raise\>       return yy::Parser::make_KW_RAISE(location());
\<with\>        return yy::Parser::make_KW_WITH(location());
\<struct\>      return yy::Parser::make_KW_STRUCT(location());
\<class\>       return yy::Parser::make_KW_CLASS(location());

\<typeof\>      return yy::Parser::make_KW_TYPEOF(location());
\<cloneof\>     return yy::Parser::make_KW_CLONEOF(location());
\<import\>      return yy::Parser::make_KW_IMPORT(location());


/*** atomic expressions ***/


{id}                                        //return make_Identifier(str(), location());

  /* int literals */
{decint}                                    return make_IntegerLiteral(str(), location());

  /* float literal */
{decint}(\.{digit}+)?([Ee][+-]?{digit}+)?   return make_FloatLiteral(str(), location());

  /* byte literals */
0x{hex}*                                    //return make_HexBytesLiteral(str(), location());

  /* bit literal */
0b{bit}*                                    //return make_BinaryBitsLiteral(str(), location());

  /* octal literal */
0o{octal}*                                  //return make_OctalBitsLiteral(str(), location());

  /* string literal */
\"(?u:{string_content})\"                   return make_StringLiteral(str(), location());

  /* raw string literal */
\`(?u:{string_content})\`                   //return make_RawStringLiteral(str(), location());


/*** comments ***/


  /* multiline comment */
"/*"{any}*?"*/"                             //ignore

  /* eol comment */
"//".*                                      //ignore


/*** control ***/


<<EOF>>                                     return yy::Parser::make_END(location());


/*** indentation ***/

^\h*\i          return yy::Parser::make_INDENT(location());
^\h*\j          return yy::Parser::make_OUTDENT(location());
\j              return yy::Parser::make_OUTDENT(location());

(?^\\\n\h*)     //ignore space after line-end backslash

  /* skip NL */
\\$             //will need to do some exclusive states here

  /* return NL, INDENT, *, NL, OUTDENT */
\\              {
                  start(INNER_BACKSLASH_READ_REST);
                  pushToken(yy::Parser::make_NL(location()));
                  return yy::Parser::make_INDENT(location());
                }

<INNER_BACKSLASH_READ_REST>\n {
                  start(INITIAL);
                  pushToken(yy::Parser::make_NL(location()));
                  return yy::Parser::make_OUTDENT(location());
                }

<INITIAL>\n     return yy::Parser::make_NL(location());

[ \t\r]         //ignore space


%%
